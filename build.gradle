import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
	id 'org.springframework.boot' version '2.6.2' apply false
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'com.google.cloud.tools.jib' version '3.1.4' apply false
}

allprojects {
	group = 'subota.max'
	version = "0.0.1"
}

ext {
	set('springCloudVersion', "2021.0.0")
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'com.google.cloud.tools.jib'

	sourceCompatibility = '17'

	dependencyManagement {
		imports {
			mavenBom SpringBootPlugin.BOM_COORDINATES
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}

		dependencies {
			dependency 'org.apache.commons:commons-lang3:3.12.0'
			dependency 'com.google.code.gson:gson:2.8.9'
			dependency 'org.junit.jupiter:junit-jupiter-api:5.7.0'
			dependency 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
		}
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
	}

	test {
		useJUnitPlatform()
	}

	jib {
		container.creationTime = "USE_CURRENT_TIMESTAMP"
		from {
			image = "bellsoft/liberica-openjdk-alpine:17"
//			platforms {
//				platform {
//					architecture = 'arm64'
//					os = 'linux'
//				}
//			}
		}

		to {
			image = project.name
			tags = ["latest"]
		}
	}

}




